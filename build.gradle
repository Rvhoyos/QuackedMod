// Root build.gradle â€” shared config for all subprojects

allprojects {
    group = rootProject.maven_group
    version = rootProject.mod_version

    repositories {
        maven {
            name = 'ParchmentMC'
            url = 'https://maven.parchmentmc.org'
        }
        exclusiveContent {
            forRepository {
                maven {
                    name = 'GeckoLib'
                    url = 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/'
                }
            }
            filter {
                includeGroup('software.bernie.geckolib')
            }
        }
    }
}

subprojects {
    apply plugin: 'maven-publish'

    afterEvaluate {
        base {
            archivesName = "$rootProject.archives_name-$project.name"
        }

        java {
            toolchain {
                languageVersion = JavaLanguageVersion.of(21)
            }
            withSourcesJar()
        }

        tasks.withType(JavaCompile).configureEach {
            it.options.release = 21
        }

        // Include common sources in platform projects
        if (project.name != 'common') {
            tasks.named('compileJava').configure {
                source project(':common').sourceSets.main.allSource
            }

            tasks.named('sourcesJar').configure {
                from project(':common').sourceSets.main.allSource
            }

            tasks.withType(ProcessResources).configureEach {
                from project(':common').sourceSets.main.resources
            }
        }

        publishing {
            publications {
                mavenJava(MavenPublication) {
                    artifactId = base.archivesName.get()
                    from components.java
                }
            }
            repositories {
            }
        }
    }
}
